plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.6.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.jetbrains.dokka' version '1.7.0'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
        url="https://repo.dmulloy2.net/content/groups/public/"
    }
    maven {
        url = "https://repo.aikar.co/nexus/content/groups/aikar/"
    }
}

dependencies {
    implementation project(':KirbyUtils-Common')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    compileOnly 'org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT'
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.4.0-SNAPSHOT'

    implementation 'co.aikar:acf-core:0.5.0-SNAPSHOT'
}

shadowJar {
    archiveClassifier = ''
    dependencies {
        include(project(':KirbyUtils-Common'))
        include(dependency('co.aikar:acf-core'))
    }
    relocate 'co.aikar.commands', 'me.mrkirby153.thirdparty.acf'
    relocate 'co.aikar.locales', 'me.mrkirby153.thirdparty.locales'
}

tasks.build.dependsOn shadowJar

java {
    withSourcesJar()
    withJavadocJar()
}

javadocJar.dependsOn dokkaJavadoc
javadocJar.from dokkaJavadoc

publishing {
    publications {
        bukkit(MavenPublication) {
            from components.java
            artifactId = 'KirbyUtils-Bukkit'
        }
    }
}